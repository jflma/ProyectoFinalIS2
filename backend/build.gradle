plugins {
	id 'java'
    id 'org.springframework.boot' version '3.3.1'
    id 'io.spring.dependency-management' version '1.1.3'
    id "org.sonarqube" version "6.0.1.5171"
    id 'io.gatling.gradle' version '3.10.3.1'
    id 'org.owasp.dependencycheck' version '9.0.9'
}

group = 'com.app'
version = '0.0.1-SNAPSHOT'
description = 'Job final for Software Ingenier'
java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

repositories {
    mavenCentral()
}

dependencies {
    // Spring Boot Dependencies
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-websocket'

    // Auth0 JWT
    implementation 'com.auth0:java-jwt:4.4.0'

    // Database Dependencies
    runtimeOnly 'org.postgresql:postgresql'

    // Hibernate Search with Lucene
    implementation 'org.hibernate.search:hibernate-search-mapper-orm:7.1.1.Final'
    implementation 'org.hibernate.search:hibernate-search-backend-lucene:7.1.1.Final'

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.34'
    annotationProcessor 'org.projectlombok:lombok:1.18.34'

    // DevTools
    developmentOnly 'org.springframework.boot:spring-boot-devtools:3.3.2'

    // Testing Dependencies
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.0-M2'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.12.0'
    testImplementation 'org.mockito:mockito-core:5.12.0'

	// H2 Database
    runtimeOnly 'com.h2database:h2:2.3.230'

    // Functional Testing
    testImplementation 'org.springframework.boot:spring-boot-test-autoconfigure'
}

sonarqube {
    properties {
        property "sonar.java.lombokSupport", "true"
    }
}
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.release = 17
}

test {
    useJUnitPlatform()
}

// Tarea para Pruebas Unitarias (excluye funcionales, seguridad y simulaciones)
task unitTest(type: Test) {
    description = 'Runs unit tests.'
    group = 'verification'
    useJUnitPlatform()
    exclude '**/*FunctionalTest.class'
    exclude '**/*SecurityTest.class'
    exclude '**/simulations/**' 
}

// Tarea para Pruebas Funcionales
task functionalTest(type: Test) {
    description = 'Runs functional tests.'
    group = 'verification'
    useJUnitPlatform()
    include '**/*FunctionalTest.class'
    shouldRunAfter unitTest
}

// Tarea para Pruebas de Seguridad
task securityTest(type: Test) {
    description = 'Runs security tests.'
    group = 'verification'
    useJUnitPlatform()
    include '**/*SecurityTest.class'
    shouldRunAfter functionalTest
}